#version 460
#extension GL_NV_ray_tracing : require


struct HitInfo
{
    bool Missed;
    vec3 Point;
    vec3 Normal;
    vec2 UV;
    vec4 Color;
    float Reflectivity;
};

layout(binding = 0) uniform accelerationStructureNV topLevelAS;
layout(binding = 1, rgba8) uniform image2D image;

layout(location = 0) rayPayloadNV HitInfo hitValue;
layout(location = 3) rayPayloadNV HitInfo reflectionHit;

#define MAX_LIGHTS 10

struct DirectionalLight
{
	vec4 Color;
	vec4 Direction;
};

struct PointLight
{
    vec4 Color;
    vec4 PositionRadius;
};

layout(binding = 2) uniform GlobalUniformData
{
    float FieldOfView;
    float NearPlane;
    float FarPlane;
    vec4 Position;
	mat4 ViewMatrix;
	mat4 ProjectionMatrix;
    vec4 ClearColor;
    vec4 LightCounts;
    PointLight PointLights[MAX_LIGHTS];
    DirectionalLight DirectionalLights[MAX_LIGHTS];
} GlobalUniform;


void FireRay(vec3 origin, vec3 direction, float tmin, float tmax)
{
    uint rayFlags = gl_RayFlagsOpaqueNV;
    uint cullMask = 0xff;
    traceNV(topLevelAS, rayFlags, cullMask, 0, 0, 0, origin, tmin, direction, tmax, 0);
}

void main() 
{
    const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeNV.xy);

    vec2 d = inUV * 2.0 - 1.0;
    float aspectRatio = float(gl_LaunchSizeNV.x) / float(gl_LaunchSizeNV.y);
    float fov = GlobalUniform.FieldOfView;
    vec3 direction = normalize(vec3(d.x * aspectRatio*fov, d.y*fov, -1));
    direction = vec3(vec4(direction,0.0f) * (GlobalUniform.ViewMatrix));

    vec3 origin = vec3(GlobalUniform.Position);
    
    FireRay(origin, direction, GlobalUniform.NearPlane, GlobalUniform.FarPlane);
    
    vec4 color = hitValue.Color;
    float prevRefl = hitValue.Reflectivity;

    if(!hitValue.Missed && hitValue.Reflectivity > 0.0f)
    {
        origin = hitValue.Point + hitValue.Normal*0.01f;
        direction = normalize(reflect(direction, hitValue.Normal));
        FireRay(origin, direction, 0.0f, GlobalUniform.FarPlane);
        color = color*(1.0f-prevRefl) + hitValue.Color*prevRefl;
    }

    imageStore(image, ivec2(gl_LaunchIDNV.xy), color);

    
}
